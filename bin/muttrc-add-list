#!/usr/bin/perl

use strict;
use warnings;

use File::Basename;
use Term::ReadLine;

use Sh qw(append_to_file);

my $dir = "~/.mutt/common.d/person-adam.spiers";
my $me = basename $0;

unless (@ARGV == 2 or @ARGV == 3) {
  die <<EOF;
Usage: $me <list short name> <list-addr> [<longer description>]

e.g. $me mercurial %s\@selenic.com '%s group'

Short name gets used as folder name and \@adamspiers.org prefix.
Longer description gets used as mutt alias description text.
Can include '%s' in address and longer description which expands to short name.
Longer description defaults to '%s mailing list'.
EOF
}

my ($short, $addr, $longer) = @ARGV;

$addr =~ s/%s/$short/g;
$longer ||= '%s mailing list';
$longer =~ s/%s/$short/g;
(my $regexp = $addr) =~ s/\@/.*/;
my $folder = $short;

my $new_stuff = <<EOTEMPLATE;
# {{{ $short

alias $short "$longer" <$addr>
#alias me.$short "Adam Spiers" <$short\@adamspiers.org>
subscribe $addr
#send-hook "$regexp" 'my_hdr From: "Adam Spiers" <$short\@adamspiers.org>'
#send-hook "$regexp" 'my_hdr From: "Adam Spiers" <aspiers\@suse.com>'
#mailboxes =in/$short
folder-hook =(in(boxes)?|lists)/$short source ~/.mutt/mailing-list

# }}}
EOTEMPLATE

my $term = Term::ReadLine->new('foo');
my $prompt = "Enter destination file: ";
my $dst = $term->readline($prompt, "$dir/50-alias-computing-community");
$dst =~ s/^~/$ENV{HOME}/;

append_to_file($dst, $new_stuff);
