#!/usr/bin/perl
#
# Wrapper for integrating mairix queries with mutt.  Supports both
# prompting for a search expression interactively, and constructing
# one based on the contents of a mail fed in via STDIN (e.g. "search
# for all mails with the same Subject: header as this one").
#
# Search syntax documented in a conflicting manner here:
#
#  http://www.rpcurnow.force9.co.uk/mairix/mairix.html#command_005fline
#
# Also run mairix -h for summary of syntax.
#
#  foo s:bar/baz=2,~qux,moo=
#  ( ) (                   )  AND <- space-delimited
#        (                 ) applies to subject only
#        ( ) (             )  OR  <- slash-delimited
#            (   ) (  ) (  )  AND <- comma-delimited
#  i.e.
#    foo AND s:(bar OR ((baz, 2 errors)  AND (NOT qux) AND (SUBSTRING moo)))

use strict;
use warnings;

$SIG{PIPE} = sub {
  warn "got SIGPIPE\n";
  sleep 2;
};

use File::Path;
use File::Which;

use Mairix;

my %fields = map { $_ => 1 } qw(subject from to message-id);

sub usage {
  die <<EOF;
Usage:
  $0 prompter
  $0 field Subject < mail
EOF
}

usage() unless @ARGV;
my $mode = shift @ARGV;

for my $var (qw/MAIRIX_RESULTS MAIRIX_DIR MAIRIXRC/) {
  unless ($ENV{$var}) {
    die "\$$var not set; aborting.\n";
  }
}

if ($ENV{MAIRIX_RESULTS} =~ /mairix/i &&
    -d "$ENV{MAIRIX_RESULTS}/cur"     &&
    -d "$ENV{MAIRIX_RESULTS}/new"     &&
    -d "$ENV{MAIRIX_RESULTS}/tmp")
{
  # We do this so that an abandoned or failed run of mairix won't
  # result in mutt still switching to an irrelevant results folder.
  # mairix will automatically recreate the results folder on success.
  rmtree($ENV{MAIRIX_RESULTS});
}

my $mairix = which('mairix');
die "Can't find executable mairix!\n" unless $mairix;

if ($mode eq 'prompt') {
  prompter();
}
elsif ($mode eq 'field') {
  usage() unless @ARGV;
  my $field = lc shift @ARGV;
  extract_field($field);
}
else {
  die "$0: Invalid mode '$mode'\n";
}

sub prompter {
  print "mairix query? ";
  chomp(my $query = <STDIN>);
  $query =~ s/^\s+//;
  $query =~ s/\s+$//;
  if ($query) {
    my @words = split /\s+/, $query;
    my $exit_code = do_query(@words);
    # mutt's wait_key mechanism will ask for a key press on failure
    exit $exit_code;
  }
  # Cancel search and return to mutt without wait_key asking for key
  # press.  This relies on the rmtree above.
  exit 0;
}

sub extract_field {
  my ($field) = @_;
  die "$0: Can't follow unsupported field '$field'\n"
    unless $fields{lc $field};

  my $value = '';
  while (<STDIN>) {
    chomp;

    last if /^$/; # no longer in header
    
    if (! $value) {
      if (/^$field:\s+(.+)/i) {
        $value = $1;
      }
      next;
    }

    if (/^\s+(.+)/) {
      # continuation of header
      $value .= " " . $1;
      next;
    }

    # continuation ended
    last;
  }

  die "$0: Didn't find '$field' field in header\n" unless $value;

  my @query = get_query($field, $value);
  my $exit_code = do_query(@query);
  # mutt's wait_key mechanism will ask for a key press on failure
  exit $exit_code;
}

sub get_query {
  my ($field, $value) = @_;

  if ($field eq 'from') {
    # extract email address and real name
    # search for real name (AND of all words) OR email address
    require Mail::Address;
    die "TODO";
  }

  if ($field eq 'subject') {
    return Mairix::subject_query($value);
  }

  if ($field eq 'message-id') {
    return Mairix::message_id_query($value);
  }

  die "BUG";
}

sub do_query {
  my (@query) = @_;
  warn "Searching for: @query\n";
  my $query_file = "$ENV{MAIRIX_DIR}/last-query";
  open(QUERY, ">$query_file")
    or die "Couldn't open($query_file): $!\n";
  print QUERY "@query\n";
  close(QUERY);
  system($mairix, '-f', $ENV{MAIRIXRC}, @query);
  return $? >> 7;
}
